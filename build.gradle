buildscript {

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.zeroturnaround:gradle-jrebel-plugin:1.1.5'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }

}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'rebel'

// See the gradle.properties to change these.
group = pluginGroup
version = pluginVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    provided
}

// Adds a provided dependency type, so that we don't get unwanted
// code in our JAR. Saves space, and confuses the ClassLoader less.
sourceSets {
    main {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

repositories {

    mavenCentral()

    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url "http://repo.mvdw-software.be/content/groups/public/" }
    maven { url "http://nexus.hc.to/content/repositories/pub_releases" }
    maven { url "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "http://ci.emc.gs/nexus/content/groups/aikar/" }

    jcenter()

}

dependencies {
    provided 'org.spigotmc:spigot-api:1.12-R0.1-SNAPSHOT'
    provided 'be.maximvdw:MVdWPlaceholderAPI:1.0.2-SNAPSHOT'
    provided 'me.clip:placeholderapi:2.0.8'
    provided 'net.milkbowl.vault:VaultAPI:1.6'

    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.zaxxer:HikariCP:2.5.1'
    compile 'co.aikar:taskchain-core:3.4.3'
    compile 'co.aikar:taskchain-bukkit:3.4.3'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
    dependencies {
        include(dependency('co.aikar:taskchain-core:3.4.3'))
        include(dependency('co.aikar:taskchain-bukkit:3.4.3'))
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {

    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }

}

build.dependsOn(shadowJar)